buildscript {
    ext.kotlin_version = '1.2.71'
    ext.playServices = "16.0.1"
    ext.supportLibraryVersion = "28.0.0"
    ext.fileProviderAuthority = "com.scurab.android.zumpareader.fileprovider"

    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        mavenCentral()
        google()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply from: 'tools.gradle'
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.scurab.zumpareader"
        buildConfigField "String", "BUILD_DETAIL", "\"build-" + getDate() + ",git-" + gitSha() + "\""
        buildConfigField "String", "Authority", "\"" + fileProviderAuthority + "\""
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 59
        versionName "3.0.6"
        manifestPlaceholders = [authority: fileProviderAuthority]
        multiDexEnabled true
    }

    buildTypes {
        debug {
            versionNameSuffix "-" + getDate()
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/java'
        main.java.srcDirs += 'src/main/kotlin'
    }

    dexOptions {
        jumboMode = true
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    jcenter()
    mavenCentral()
}

task (depsize) << {
    def size = 0;
    configurations._debugApk.collect { it.length() / (1024 * 1024) }.each { size += it }
    println "Total dependencies size: ${Math.round(size * 100) / 100} Mb"

    configurations
            ._debugApk
            .sort { -it.length() }
            .each { println "${it.name} : ${Math.round(it.length() / (1024) * 100) / 100} kb" }
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.google.android.gms:play-services-base:${playServices}"
    implementation 'com.google.firebase:firebase-messaging:17.3.3'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"

    implementation 'org.jetbrains.anko:anko-sdk15:0.8.3' // sdk19, sdk21, sdk23 are also available
    implementation 'org.jetbrains.anko:anko-support-v4:0.8.3' // In case you need support-v4 bindings
    implementation 'org.jetbrains.anko:anko-appcompat-v7:0.8.3' // For appcompat-v7 bindings

    implementation 'org.jsoup:jsoup:1.11.2'
    implementation 'com.pawegio.kandroid:kandroid:0.8.2@aar'

    def square = "3.11.0"
    implementation "com.squareup.okhttp3:okhttp:${square}"
    implementation "com.squareup.okhttp3:okhttp-urlconnection:${square}"
    implementation "com.squareup.okhttp3:logging-interceptor:${square}"

    def retrofit = "2.4.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"

    implementation 'com.github.orangegangsters:swipy:1.2.3@aar'

    implementation 'com.squareup.picasso:picasso:2.5.2'

    implementation 'com.github.salomonbrys.kotson:kotson:2.5.0'
    implementation 'com.squareup:otto:1.3.8'

    implementation "io.reactivex.rxjava2:rxjava:2.2.2"
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-android:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.1.0'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-navi:2.0.1'
    
    implementation('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }

    implementation 'com.facebook.fresco:fresco:1.5.0'
    implementation 'com.facebook.fresco:animated-gif:1.5.0'

    implementation 'com.bogdwellers:pinchtozoom:0.1'

    implementation 'com.bugfender.sdk:android:1.+'

    implementation('com.giphy.sdk:core:1.0.0@aar') {
        transitive=true
    }

    testImplementation 'junit:junit:4.12'
    def robo = "3.3.1"
    testImplementation "org.robolectric:robolectric:$robo"
    testImplementation "org.robolectric:shadows-support-v4:$robo"
    testImplementation ('com.squareup:fest-android:1.0.8') {
        exclude module: 'support-v4'//some weird incompatibility issue
    }
    testImplementation 'org.mockito:mockito-core:2.11.0'
}
